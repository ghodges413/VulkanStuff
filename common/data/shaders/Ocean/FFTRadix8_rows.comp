#version 450
#include "FFTTwiddles.csh"

#define WORK_GROUP_SIZE 128
layout ( local_size_x = WORK_GROUP_SIZE, local_size_y = 1, local_size_z = 1 ) in;

layout( binding = 0, rg32f ) uniform readonly image2D sourceTex;
layout ( binding = 1 ) uniform writeonly image2D destTex;

uniform int N;
uniform int Ns;
uniform int K_W;

/*
– N = Number of complex input points
– gId = Global ID of a work item
– gSize = Global work size (Total number of work items)
– Idout = Output index of each work item
– Ns = Parameter that defines output index
– K_W = Kernel Size
*/

/*
 ==========================
 main
 ==========================
 */
void main() {
	//gl_LocalInvocationID
	//gl_WorkGroupID
	int width = N / 8;
	int height = N;
	int thread_id = int( gl_GlobalInvocationID.x );

	// We might be running more threads than we have data, don't run the program on phantom data
//	if ( thread_id >= N * N / 8 ) {
//		return;
//	}
	
	int rowOffset = int( thread_id / width );
	int gId = thread_id % ( width );
	int gSize = N / 8;
	
	vec2 data[ 8 ];

	int idxIn = gId;
	data[ 0 ] = imageLoad( sourceTex, ivec2( ( 0 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 1 ] = imageLoad( sourceTex, ivec2( ( 1 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 2 ] = imageLoad( sourceTex, ivec2( ( 2 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 3 ] = imageLoad( sourceTex, ivec2( ( 3 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 4 ] = imageLoad( sourceTex, ivec2( ( 4 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 5 ] = imageLoad( sourceTex, ivec2( ( 5 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 6 ] = imageLoad( sourceTex, ivec2( ( 6 * gSize ) + idxIn, rowOffset ) ).xy;
	data[ 7 ] = imageLoad( sourceTex, ivec2( ( 7 * gSize ) + idxIn, rowOffset ) ).xy;

	if ( Ns != 1 ) {
		//float angle = -2.0 * PI * float( gId ) / float( N );
		float angle = -2.0 * PI * ( gId % Ns ) / ( Ns * K_W );

		//data[ 0 ] = ComplexMultiply( data[ 0 ], Complex( 0.0 * angle ) ); // pointless to actually run this, as it has zero effect on the input/output
		data[ 1 ] = ComplexMultiply( data[ 1 ], Complex( 1.0 * angle ) );
		data[ 2 ] = ComplexMultiply( data[ 2 ], Complex( 2.0 * angle ) );
		data[ 3 ] = ComplexMultiply( data[ 3 ], Complex( 3.0 * angle ) );
		data[ 4 ] = ComplexMultiply( data[ 4 ], Complex( 4.0 * angle ) );
		data[ 5 ] = ComplexMultiply( data[ 5 ], Complex( 5.0 * angle ) );
		data[ 6 ] = ComplexMultiply( data[ 6 ], Complex( 6.0 * angle ) );
		data[ 7 ] = ComplexMultiply( data[ 7 ], Complex( 7.0 * angle ) );
	}

	// Maths
	FFT8( data );
	
	int Idout = ( gId / Ns ) * Ns * K_W + ( gId % Ns );
	imageStore( destTex, ivec2( ( 0 * Ns ) + Idout, rowOffset ), vec4( data[ 0 ].x, data[ 0 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 1 * Ns ) + Idout, rowOffset ), vec4( data[ 1 ].x, data[ 1 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 2 * Ns ) + Idout, rowOffset ), vec4( data[ 2 ].x, data[ 2 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 3 * Ns ) + Idout, rowOffset ), vec4( data[ 3 ].x, data[ 3 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 4 * Ns ) + Idout, rowOffset ), vec4( data[ 4 ].x, data[ 4 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 5 * Ns ) + Idout, rowOffset ), vec4( data[ 5 ].x, data[ 5 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 6 * Ns ) + Idout, rowOffset ), vec4( data[ 6 ].x, data[ 6 ].y, 0.0, 1.0 ) );
	imageStore( destTex, ivec2( ( 7 * Ns ) + Idout, rowOffset ), vec4( data[ 7 ].x, data[ 7 ].y, 0.0, 1.0 ) );
}


 
 